buildscript {
    dependencies {
        classpath "org.robovm:robovm-gradle-plugin:$roboVMVersion"
    }
}

apply plugin: 'java'
apply plugin: 'robovm'
apply plugin: 'idea'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

sourceSets {
    dagger {
        java {
            srcDirs = ['gen/dagger/java']
        }
    }
}

idea {
    module {
        generatedSourceDirs += file('gen/dagger/java')
    }
}

configurations {
    compileDagger
}

compileJava {
    description = "dagger annotation processor is loaded automatically from classpath"
    sourceSets.dagger.java.srcDirs*.mkdirs()
    classpath += configurations.compileDagger
    options.compilerArgs += [
            '-s', sourceSets.dagger.java.srcDirs.iterator().next()
    ]
}

clean {
    description = "delete files in generated source directory tree"
    delete fileTree(dir: sourceSets.dagger.java.srcDirs.iterator().next())
}

dependencies {
    compile project(':client-shared')

    compile "com.google.dagger:dagger:$daggerVersion"
    compileDagger "com.google.dagger:dagger-compiler:$daggerVersion"

    compile "org.robovm:robovm-rt:$roboVMVersion"
    compile "org.robovm:robovm-cocoatouch:$roboVMVersion"
}

launchIPhoneSimulator.dependsOn build
launchIPadSimulator.dependsOn build
launchIOSDevice.dependsOn build
createIPA.dependsOn build